area TempleOfTheOceanKing:
  room B2:
    node Upper:
      door ToB1
      enemy StationaryPhantom

    node SummonKey:
      set KeySpawned

    Upper <-> SummonKey: (macro Sword | item Boomerang | item Bow | item Hammer | macro Bombs | item GrapplingHook)

    node Lower:
      enemy PatrollingPhantom

    Upper <-> Lower: (item Boomerang | item Bow | item Hammer | macro Bombs)

    node MiddleSwitch:
      gain SwitchRed
      gain SwitchBlue

    node UpperToMiddleSwitch:
      lose SwitchRed

    node LowerToMiddleSwitch:
      lose SwitchBlue

    Upper <-> UpperToMiddleSwitch: state SwitchBlue
    UpperToMiddleSwitch <-> MiddleSwitch: state SwitchBlue

    Lower <-> LowerToMiddleSwitch: state SwitchRed
    LowerToMiddleSwitch <-> MiddleSwitch: state SwitchRed

    node KeyArea
    node FreestandingKey:
      chest Key

    Lower <-> KeyArea: item Boomerang
    KeyArea <-> FreestandingKey: flag KeySpawned

    node DownstairsLockedDoor:
      lock DownstairsLockedDoor

    node Downstairs:
      door ToB3

    Upper -> DownstairsLockedDoor
    Downstairs -> DownstairsLockedDoor
    DownstairsLockedDoor -> Upper: open DownstairsLockedDoor
    DownstairsLockedDoor -> Downstairs: open DownstairsLockedDoor

    node AllDefeatChest:
      chest Killer

    Lower <-> AllDefeatChest: (defeated StationaryPhantom & defeated PatrollingPhantom)

    node BombchuChest:
      chest BombchuPath

    Lower <-> BombchuChest: item Bombchus
